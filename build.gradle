plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

// Common configuration for all sub-projects
allprojects {
    group = 'com.app'
    version = '0.0.1-SNAPSHOT'
    
    repositories {
        mavenCentral()
    }
}

// Configuration for all sub-projects (excludes root project)
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(24)
        }
    }
    
    ext {
        set('springCloudVersion', "2025.0.0")
    }
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    
    tasks.named('test') {
        useJUnitPlatform()
    }
    
    // Common dependencies for all sub-projects
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
